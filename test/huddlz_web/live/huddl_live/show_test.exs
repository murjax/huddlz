defmodule HuddlzWeb.HuddlLive.ShowTest do
  use HuddlzWeb.ConnCase, async: true

  import Huddlz.Test.Helpers.Authentication
  import Huddlz.Generator

  alias Huddlz.Accounts.User
  alias Huddlz.Communities
  alias Huddlz.Communities.Group
  alias Huddlz.Communities.GroupMember
  alias Huddlz.Communities.Huddl
  alias Huddlz.Communities.HuddlCoverImage
  alias Huddlz.Communities.HuddlTemplate

  describe "Show huddl details" do
    setup do
      owner = create_verified_user()
      member = create_verified_user()
      non_member = create_verified_user()

      # Create a public group
      group =
        Group
        |> Ash.Changeset.for_create(
          :create_group,
          %{
            name: "Test Group",
            description: "A test group for huddl show",
            location: "Saint Augustine, FL",
            time_zone: "America/New_York",
            is_public: true
          },
          actor: owner
        )
        |> Ash.create!()

      # Add member to group
      GroupMember
      |> Ash.Changeset.for_create(
        :add_member,
        %{
          group_id: group.id,
          user_id: member.id,
          role: "member"
        },
        actor: owner
      )
      |> Ash.create!()

      # Create a virtual huddl
      huddl =
        Huddl
        |> Ash.Changeset.for_create(
          :create,
          %{
            title: "Virtual Meeting",
            description: "Join us for an online discussion",
            date: Date.add(Date.utc_today(), 1),
            start_time: ~T[14:00:00],
            duration_minutes: 120,
            event_type: :virtual,
            virtual_link: "https://zoom.us/j/123456789",
            is_private: false,
            group_id: group.id
          },
          actor: owner
        )
        |> Ash.create!()

      %{
        owner: owner,
        member: member,
        non_member: non_member,
        group: group,
        huddl: huddl
      }
    end

    test "links to the hosting group from the hero and the sidebar", %{
      conn: conn,
      group: group,
      huddl: huddl
    } do
      session = visit(conn, "/groups/#{group.slug}/huddlz/#{huddl.id}")

      session
      |> assert_has("#huddl-hero-group[href='/groups/#{group.slug}']", text: "Test Group")
      |> assert_has("#huddl-hero-group .group-mark", text: "TG", exact: true)
      |> assert_has(".hero-fallback span", text: "TG", exact: true)
      |> assert_has("#huddl-group-link[href='/groups/#{group.slug}']", text: "Test Group")
      |> assert_has("#huddl-group-link .group-row-meta", text: "Saint Augustine, FL")
      |> assert_has("#huddl-group-link .group-cover-signal", text: "TG")
      |> assert_has("#huddl-group .creator-row", text: "Organized by")
    end

    test "renders v3 chrome with hero and huddl-frame", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has("aside.sidebar")
      |> assert_has(".hero h1", text: huddl.title)
      |> assert_has(".hero .eyebrow", text: "Online huddl")
      |> assert_has(".hero .eyebrow", text: "Upcoming")
      |> assert_has(".huddl-frame .huddl-intro.prose p", text: huddl.description)
      |> assert_has("aside.huddl-side h3", text: "RSVP")
      |> assert_has(".facts .label", text: "When")
      |> assert_has(".facts .label", text: "Virtual access")
      |> assert_has(".facts .label", text: "Capacity")
      |> assert_has(".facts .value", text: "1 person attending")
    end

    test "sidebar share section offers a mailto email link and a QR code modal", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      huddl_url = HuddlzWeb.Endpoint.url() <> ~p"/groups/#{group.slug}/huddlz/#{huddl.id}"

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has("aside.huddl-side h3", text: "Share")
      |> assert_has("#share-huddl-modal-email[href^='mailto:?subject=Virtual%20Meeting']")
      |> assert_has("#share-huddl-modal-open[phx-click*='share-huddl-modal']")
      |> assert_has("#share-huddl-modal-url[value='#{huddl_url}']")
      |> assert_has(
        "#share-huddl-modal-copy[data-copy-target='#share-huddl-modal-url'] #share-huddl-modal-copy-label[phx-hook='ClipboardCopy'][phx-update='ignore']"
      )
      |> assert_has("#share-huddl-modal .qr-frame svg")
    end

    test "share link works for signed-out visitors", %{conn: conn, group: group, huddl: huddl} do
      huddl_url = HuddlzWeb.Endpoint.url() <> ~p"/groups/#{group.slug}/huddlz/#{huddl.id}"

      conn
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has("#share-huddl-modal-email[href^='mailto:?subject=Virtual%20Meeting']")
      |> assert_has("#share-huddl-modal-url[value='#{huddl_url}']")
    end

    test "renders image fallback behavior across huddl surfaces", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      HuddlCoverImage
      |> Ash.Changeset.for_create(:create, %{
        filename: "cover.jpg",
        content_type: "image/jpeg",
        size_bytes: 123,
        storage_path: "/uploads/huddl_cover_images/#{huddl.id}/cover.jpg",
        thumbnail_path: "/uploads/huddl_cover_images/#{huddl.id}/cover_thumb.jpg",
        huddl_id: huddl.id
      })
      |> Ash.create!(authorize?: false)

      Communities.rsvp_huddl!(huddl, actor: member)

      image_fallback_attributes = ".cover-image[aria-hidden='true'][style]"

      session =
        conn
        |> login(member)
        |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> assert_has("#huddl-cover-#{huddl.id}#{image_fallback_attributes}")

      session
      |> visit(~p"/discover")
      |> assert_has("#huddl-card-cover-#{huddl.id}#{image_fallback_attributes}")
      |> visit(~p"/groups/#{group.slug}")
      |> assert_has("#group-huddl-card-cover-#{huddl.id}#{image_fallback_attributes}")
    end

    test "renders rich link preview metadata", %{conn: conn, group: group, huddl: huddl} do
      html =
        conn
        |> get(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> html_response(200)

      assert meta_content(html, ~s(meta[property="og:type"])) == "event"

      assert meta_content(html, ~s(meta[property="og:title"])) =~
               ~r/^#{Regex.escape(huddl.title)} · \w{3}, \w{3} \d{1,2}, \d{4} · \d{1,2}:\d{2} [AP]M E[DS]T$/

      assert meta_content(html, ~s(meta[name="description"])) == huddl.description
      assert meta_content(html, ~s(meta[property="og:description"])) == huddl.description
      assert meta_content(html, ~s(meta[name="twitter:description"])) == huddl.description

      assert meta_content(html, ~s(meta[property="og:url"])) =~
               "/groups/#{group.slug}/huddlz/#{huddl.id}"
    end

    test "renders rich link preview image metadata", %{conn: conn, group: group, huddl: huddl} do
      thumbnail_path = "/uploads/huddl_cover_images/#{huddl.id}/preview_thumb.jpg"

      HuddlCoverImage
      |> Ash.Changeset.for_create(:create, %{
        filename: "preview.jpg",
        content_type: "image/jpeg",
        size_bytes: 123,
        storage_path: "/uploads/huddl_cover_images/#{huddl.id}/preview.jpg",
        thumbnail_path: thumbnail_path,
        huddl_id: huddl.id
      })
      |> Ash.create!(authorize?: false)

      html =
        conn
        |> get(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> html_response(200)

      assert meta_content(html, ~s(meta[property="og:image"])) ==
               HuddlzWeb.Endpoint.url() <> thumbnail_path

      assert meta_content(html, ~s(meta[name="twitter:image"])) ==
               HuddlzWeb.Endpoint.url() <> thumbnail_path

      assert meta_content(html, ~s(meta[name="twitter:card"])) == "summary_large_image"
    end

    test "renders fallback description metadata", %{conn: conn, group: group, owner: owner} do
      huddl =
        Huddl
        |> Ash.Changeset.for_create(
          :create,
          %{
            title: "Description-free Huddl",
            description: nil,
            date: Date.add(Date.utc_today(), 2),
            start_time: ~T[14:00:00],
            duration_minutes: 120,
            event_type: :in_person,
            group_location_id: address_book_location_id(group.id),
            is_private: false,
            group_id: group.id
          },
          actor: owner
        )
        |> Ash.create!()

      html =
        conn
        |> get(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> html_response(200)

      assert meta_content(html, ~s(meta[name="description"])) ==
               "Find and join this huddl on huddlz."
    end

    test "shows RSVP button for authenticated users", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has("button", text: "RSVP to this huddl")
      |> refute_has(".rsvp-banner.cyan", text: "You're attending")
      # Click RSVP button
      |> click_button("RSVP to this huddl")
      # Check UI updates after RSVP
      |> assert_has(".rsvp-banner.cyan", text: "You're attending")
      |> refute_has("button", text: "RSVP to this huddl")
      # The creator and member are both attending.
      |> assert_has(".facts .value", text: "2 people attending")
    end

    test "docks the RSVP block with a share shortcut while there is something to do", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      session =
        conn
        |> login(member)
        |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> assert_has(".rsvp-state[data-dock] .rsvp-cta", text: "RSVP to this huddl")
        |> assert_has(
          ".rsvp-state[data-dock] #huddl-rsvp-share[aria-label='Share this huddl'][phx-click*='share-huddl-modal']"
        )

      Communities.cancel_huddl!(huddl, "Cancelled", actor: owner)

      session
      |> assert_has(".rsvp-state .rsvp-banner", text: "cancelled")
      |> refute_has(".rsvp-state[data-dock]")
      |> refute_has("#huddl-rsvp-share")
    end

    test "shows virtual link after RSVP", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      # Before RSVP, virtual link is not visible
      |> assert_has(".facts .value .muted", text: "Virtual link available after RSVP")
      |> refute_has("a", text: "Join virtually")
      # RSVP
      |> click_button("RSVP to this huddl")
      # After RSVP, virtual link is visible
      |> assert_has("a.virtual-link-text", text: "Join virtually")
    end

    test "hides the virtual link while waitlisted and reveals it on promotion", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      Communities.update_huddl!(huddl, %{max_attendees: 1}, actor: owner)

      session =
        conn
        |> login(member)
        |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> click_button("Join waitlist")
        |> assert_has(".rsvp-banner.warn", text: "On waitlist")
        |> assert_has(
          ".facts .value .muted",
          text: "Virtual link available when your RSVP is confirmed"
        )
        |> refute_has("a.virtual-link-text", text: "Join virtually")

      Communities.cancel_rsvp_huddl!(huddl, actor: owner)

      session
      |> assert_has(".rsvp-banner.cyan", text: "You're attending")
      |> assert_has("a.virtual-link-text", text: "Join virtually")
      |> click_button("Cancel RSVP")
      |> assert_has(".facts .value .muted", text: "Virtual link available after RSVP")
      |> refute_has("a.virtual-link-text", text: "Join virtually")
    end

    test "capacity promotion and huddl cancellation update an open attendee page", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      Communities.update_huddl!(huddl, %{max_attendees: 1}, actor: owner)
      Communities.join_waitlist_huddl!(huddl, actor: member)

      session =
        conn
        |> login(member)
        |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> assert_has(".rsvp-banner.warn", text: "On waitlist")
        |> refute_has("a.virtual-link-text")

      Communities.update_huddl!(huddl, %{max_attendees: 2}, actor: owner)

      session =
        session
        |> assert_has(".rsvp-banner.cyan", text: "You're attending")
        |> assert_has("a.virtual-link-text")

      Communities.cancel_huddl!(huddl, "Cancelled", actor: owner)

      session
      |> assert_has(".hero .eyebrow", text: "Cancelled")
      |> refute_has("a.virtual-link-text")
    end

    test "prevents duplicate RSVPs", %{conn: conn, member: member, group: group, huddl: huddl} do
      # First RSVP
      updated_huddl =
        huddl
        |> Ash.Changeset.for_update(:rsvp, %{}, actor: member)
        |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{updated_huddl.id}")
      # Should already show as attending
      |> assert_has(".rsvp-banner.cyan", text: "You're attending")
      |> refute_has("button", text: "RSVP to this huddl")
      |> assert_has(".facts .value", text: "2 people attending")
    end

    test "shows correct attendee count with multiple RSVPs", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      # Owner RSVPs
      huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: owner)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has(".facts .value", text: "1 person attending")
      # Member RSVPs
      |> click_button("RSVP to this huddl")
      |> assert_has(".facts .value", text: "2 people attending")
    end

    test "shows capacity status for limited huddls", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      limited_huddl =
        huddl
        |> Ash.Changeset.for_update(:update, %{max_attendees: 2}, actor: owner)
        |> Ash.update!()

      limited_huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: owner)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{limited_huddl.id}")
      |> assert_has(".facts .value", text: "1/2 spots filled")
      |> assert_has(".facts .value", text: "Filling up")
      |> assert_has("button", text: "RSVP to this huddl")
    end

    test "shows almost-full status when capacity is at the 80% threshold", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      capped =
        huddl
        |> Ash.Changeset.for_update(:update, %{max_attendees: 5}, actor: owner)
        |> Ash.update!()

      for actor <- [owner, create_verified_user(), create_verified_user(), create_verified_user()] do
        capped
        |> Ash.reload!()
        |> Ash.Changeset.for_update(:rsvp, %{}, actor: actor)
        |> Ash.update!()
      end

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{capped.id}")
      |> assert_has(".facts .value", text: "4/5 spots filled")
      |> assert_has(".facts .value", text: "Almost full")
      |> assert_has("button", text: "RSVP to this huddl")
    end

    test "shows event full instead of RSVP button when capacity is reached", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      full_huddl =
        huddl
        |> Ash.Changeset.for_update(:update, %{max_attendees: 1}, actor: owner)
        |> Ash.update!()

      full_huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: owner)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{full_huddl.id}")
      |> assert_has(".rsvp-banner.warn", text: "This huddl is full")
      |> assert_has(".facts .value", text: "1/1 spots filled")
      |> refute_has("button", text: "RSVP to this huddl")
      |> assert_has("button[phx-disable-with='Joining waitlist...']", text: "Join waitlist")
    end

    test "non-authenticated users see sign-in prompt for virtual link", %{
      conn: conn,
      group: group,
      huddl: huddl
    } do
      conn
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has(".facts .value .muted", text: "Sign in and RSVP to get virtual link")
      |> refute_has("button", text: "RSVP to this huddl")
      |> assert_has(
        "a[href='/sign-in?return_to=%2Fgroups%2F#{group.slug}%2Fhuddlz%2F#{huddl.id}']",
        text: "Sign in to RSVP"
      )
    end

    test "handles different huddl types correctly", %{
      conn: conn,
      owner: owner,
      non_member: non_member,
      group: group
    } do
      # Create in-person huddl
      in_person_huddl =
        Huddl
        |> Ash.Changeset.for_create(
          :create,
          %{
            title: "In-Person Meetup",
            description: "Meet us at the coffee shop",
            date: Date.add(Date.utc_today(), 1),
            start_time: ~T[14:00:00],
            duration_minutes: 120,
            event_type: :in_person,
            group_location_id: address_book_location_id(group.id),
            is_private: false,
            group_id: group.id
          },
          actor: owner
        )
        |> Ash.create!()

      conn
      |> login(non_member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{in_person_huddl.id}")
      |> assert_has(".facts .value", text: "123 Main St, Anytown, USA")
      |> assert_has(
        "a.map-link[href='https://www.google.com/maps/search/?api=1&query=123+Main+St%2C+Anytown%2C+USA']",
        text: "View on map"
      )
      |> refute_has(".facts .label", text: "Virtual access")

      # Create hybrid huddl
      hybrid_huddl =
        Huddl
        |> Ash.Changeset.for_create(
          :create,
          %{
            title: "Hybrid Event",
            description: "Join us in person or online",
            date: Date.add(Date.utc_today(), 2),
            start_time: ~T[15:00:00],
            duration_minutes: 120,
            event_type: :hybrid,
            group_location_id: address_book_location_id(group.id),
            virtual_link: "https://meet.example.com/hybrid",
            is_private: false,
            group_id: group.id
          },
          actor: owner
        )
        |> Ash.create!()

      conn
      |> login(non_member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{hybrid_huddl.id}")
      |> assert_has(".facts .value", text: "123 Main St, Anytown, USA")
      |> assert_has(
        "a.map-link[href='https://www.google.com/maps/search/?api=1&query=123+Main+St%2C+Anytown%2C+USA']",
        text: "View on map"
      )
      |> assert_has(".facts .label", text: "Virtual access")
      |> assert_has(".facts .value .muted", text: "Virtual link available after RSVP")
      # RSVP to see virtual link
      |> click_button("RSVP to this huddl")
      |> assert_has("a.virtual-link-text", text: "Join virtually")
    end

    test "private huddl is indistinguishable from a missing huddl", %{
      conn: conn,
      non_member: non_member,
      owner: owner
    } do
      # Create private group
      private_group =
        Group
        |> Ash.Changeset.for_create(
          :create_group,
          %{
            name: "Private Group",
            description: "Members only",
            location: "Saint Augustine, FL",
            time_zone: "America/New_York",
            is_public: false
          },
          actor: owner
        )
        |> Ash.create!()

      # Create private huddl
      private_huddl =
        Huddl
        |> Ash.Changeset.for_create(
          :create,
          %{
            title: "Private Event",
            description: "Members only event",
            date: Date.add(Date.utc_today(), 3),
            start_time: ~T[16:00:00],
            duration_minutes: 120,
            event_type: :in_person,
            group_location_id: address_book_location_id(private_group.id),
            is_private: true,
            group_id: private_group.id
          },
          actor: owner
        )
        |> Ash.create!()

      assert {404, _headers, body} =
               assert_error_sent(404, fn ->
                 conn
                 |> login(non_member)
                 |> get(~p"/groups/#{private_group.slug}/huddlz/#{private_huddl.id}")
               end)

      assert body =~ "This path doesn’t lead to a huddl."
      refute body =~ to_string(private_huddl.title)
    end

    test "shows Cancel RSVP button when user has RSVPed", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      # First RSVP to the huddl
      huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: member)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      # Should show Cancel RSVP button instead of RSVP button
      |> assert_has("button", text: "Cancel RSVP")
      |> refute_has("button", text: "RSVP to this huddl")
      # Should still show attending status
      |> assert_has(".rsvp-banner.cyan", text: "You're attending")
    end

    test "Cancel RSVP button only shows for upcoming huddls", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group
    } do
      # Create a past huddl
      past_huddl =
        generate(
          past_huddl(
            title: "Past Event",
            description: "This already happened",
            starts_at: DateTime.add(DateTime.utc_now(), -2, :day),
            ends_at: DateTime.add(DateTime.utc_now(), -1, :day),
            event_type: :virtual,
            virtual_link: "https://zoom.us/j/past",
            is_private: false,
            group_id: group.id,
            creator_id: owner.id
          )
        )

      # RSVP to the past huddl (directly in database since it's past)
      past_huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: member)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{past_huddl.id}")
      # Should not show Cancel RSVP button for past events
      |> refute_has("button", text: "Cancel RSVP")
      |> refute_has("button", text: "RSVP to this huddl")
      # But should still show attended status for past event
      |> assert_has(".facts .value", text: "1 person attended")
    end

    test "handles cancel_rsvp event successfully", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      # First RSVP to the huddl
      huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: member)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      # Click Cancel RSVP button
      |> click_button("Cancel RSVP")
      # Check UI updates after cancel
      |> assert_has("button", text: "RSVP to this huddl")
      |> refute_has(".rsvp-banner.cyan", text: "You're attending")
      |> refute_has("button", text: "Cancel RSVP")
      # The creator remains attending.
      |> assert_has(".facts .value", text: "1 person attending")
    end

    test "cancel_rsvp updates attendee count correctly", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      # Both users RSVP
      huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: owner)
      |> Ash.update!()

      huddl
      |> Ash.reload!()
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: member)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has(".facts .value", text: "2 people attending")
      # Member cancels their RSVP
      |> click_button("Cancel RSVP")
      # Should show 1 person attending (owner still RSVPed)
      |> assert_has(".facts .value", text: "1 person attending")
    end

    test "can RSVP again after cancelling", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      # RSVP
      huddl
      |> Ash.Changeset.for_update(:rsvp, %{}, actor: member)
      |> Ash.update!()

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      # Cancel RSVP
      |> click_button("Cancel RSVP")
      # RSVP again
      |> click_button("RSVP to this huddl")
      # Check UI updates after second RSVP
      |> assert_has(".rsvp-banner.cyan", text: "You're attending")
      |> assert_has(".facts .value", text: "2 people attending")
    end

    test "rendering type and status correctly", %{conn: conn, owner: owner, group: group} do
      # Create in-person in-progress huddl
      in_person_huddl =
        generate(
          past_huddl(
            title: "In-Person Meetup",
            description: "Meet us at the coffee shop",
            starts_at: DateTime.add(DateTime.utc_now(), -1, :hour),
            ends_at: DateTime.add(DateTime.utc_now(), 1, :hour),
            event_type: :in_person,
            is_private: false,
            group_id: group.id,
            creator_id: owner.id
          )
        )

      conn
      |> login(owner)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{in_person_huddl.id}")
      |> assert_has(".eyebrow", text: "In-person huddl")
      |> assert_has(".eyebrow", text: "Happening now")
    end

    test "shows completed banner and 'attended' copy for past huddlz", %{
      conn: conn,
      member: member,
      owner: owner,
      group: group
    } do
      completed_huddl =
        generate(
          past_huddl(
            title: "Wrapped-up workshop",
            description: "Already happened",
            starts_at: DateTime.add(DateTime.utc_now(), -2, :day),
            ends_at: DateTime.add(DateTime.utc_now(), -1, :day),
            event_type: :virtual,
            virtual_link: "https://zoom.us/j/old",
            is_private: false,
            group_id: group.id,
            creator_id: owner.id
          )
        )

      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{completed_huddl.id}")
      |> assert_has(".eyebrow", text: "Completed")
      |> assert_has(".rsvp-banner.muted", text: "This huddl has ended")
      |> assert_has(".facts .label", text: "Attended")
      |> assert_has(".facts .value .muted", text: "Link expired")
      |> refute_has("button", text: "RSVP to this huddl")
      |> refute_has("button", text: "Cancel RSVP")
    end

    test "hides Organize section from non-owners", %{
      conn: conn,
      member: member,
      group: group,
      huddl: huddl
    } do
      # "Organized by" is always shown; assert the Organize action stack is gone instead.
      conn
      |> login(member)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> refute_has("a", text: "Edit huddl")
      |> refute_has("button", text: "Cancel huddl")
      |> refute_has("button", text: "Delete huddl")
    end

    test "shows Organize section with edit and cancel for owner", %{
      conn: conn,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      conn
      |> login(owner)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> assert_has(".huddl-side-section h3", text: "Organize")
      |> assert_has("a", text: "Edit huddl")
      |> assert_has("button", text: "Cancel huddl")
      |> refute_has("button", text: "Publish huddl")
      |> refute_has("button", text: "Delete huddl")
    end

    test "hides lifecycle actions after the huddl ends", %{
      conn: conn,
      owner: owner,
      group: group
    } do
      now = DateTime.utc_now()

      ended =
        Ash.Seed.seed!(Huddl, %{
          time_zone: "America/New_York",
          title: "Already Ended",
          description: "Waiting for scheduled completion",
          starts_at: DateTime.add(now, -2, :hour),
          ends_at: DateTime.add(now, -1, :hour),
          event_type: :virtual,
          virtual_link: "https://example.com/ended",
          is_private: false,
          group_id: group.id,
          creator_id: owner.id,
          lifecycle_state: :published,
          published_at: DateTime.add(now, -3, :hour),
          published_by_id: owner.id
        })

      conn
      |> login(owner)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{ended.id}")
      |> assert_has(".eyebrow", text: "Completed")
      |> refute_has("button", text: "Publish huddl")
      |> refute_has("button", text: "Cancel huddl")
      |> refute_has("a", text: "Edit huddl")
    end

    test "opens and dismisses the styled cancellation confirmation", %{
      conn: conn,
      owner: owner,
      group: group,
      huddl: huddl
    } do
      conn
      |> login(owner)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      |> refute_has("#cancel-huddl-modal")
      |> click_button("Cancel huddl")
      |> assert_has("#cancel-huddl-modal [role='dialog']")
      |> assert_has("#cancel-huddl-modal-title", text: "Cancel this huddl?")
      |> assert_has("#cancel-huddl-modal", text: huddl.title)
      |> assert_has("#cancel-huddl-modal", text: "remain in calendars and RSVP history")
      |> assert_has("#cancel-huddl-modal [role='dialog'][aria-modal='true'][tabindex='0']")
      |> assert_has(
        "#cancel-huddl-modal-container[phx-key='escape'][phx-window-keydown][phx-click-away]"
      )
      |> assert_has("#open-cancel-huddl-modal[phx-click*='push_focus']")
      |> within("#cancel-huddl-modal", fn session ->
        click_button(session, "Keep huddl")
      end)
      |> refute_has("#cancel-huddl-modal")
      |> assert_path(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")

      assert huddl_still_exists?(huddl.id)
    end

    test "explains that deleting a recurring huddl leaves the series intact", %{
      conn: conn,
      owner: owner,
      group: group
    } do
      template =
        HuddlTemplate
        |> Ash.Changeset.for_create(:create, %{
          frequency: :weekly,
          repeat_until: DateTime.add(DateTime.utc_now(), 30, :day),
          starts_at_local: NaiveDateTime.new!(Date.add(Date.utc_today(), 2), ~T[14:00:00]),
          ends_at_local: NaiveDateTime.new!(Date.add(Date.utc_today(), 2), ~T[15:00:00]),
          time_zone: "America/New_York"
        })
        |> Ash.create!(authorize?: false)

      recurring_huddl =
        Huddl
        |> Ash.Changeset.for_create(
          :create,
          %{
            title: "Weekly Workshop",
            description: "A recurring workshop",
            date: Date.add(Date.utc_today(), 2),
            start_time: ~T[14:00:00],
            duration_minutes: 60,
            event_type: :virtual,
            virtual_link: "https://example.com/weekly-workshop",
            is_private: false,
            group_id: group.id,
            huddl_template_id: template.id,
            lifecycle_state: :draft
          },
          actor: owner
        )
        |> Ash.create!()

      conn
      |> login(owner)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{recurring_huddl.id}")
      |> click_button("Delete huddl")
      |> assert_has(
        "#delete-huddl-modal .delete-confirm-series-note",
        text: "This deletes only the selected occurrence"
      )
      |> assert_has(
        "#delete-huddl-modal .delete-confirm-series-note",
        text: "Other occurrences in the recurring series will remain"
      )
    end

    test "deletes the huddl only after modal confirmation", %{
      conn: conn,
      owner: owner,
      group: group,
      huddl: _huddl
    } do
      draft =
        generate(
          huddl(
            group_id: group.id,
            creator_id: owner.id,
            lifecycle_state: :draft
          )
        )

      conn
      |> login(owner)
      |> visit(~p"/groups/#{group.slug}/huddlz/#{draft.id}")
      |> click_button("Delete huddl")
      |> within("#delete-huddl-modal", fn session ->
        click_button(session, "Delete huddl")
      end)
      |> assert_path(~p"/groups/#{group.slug}")

      deleted =
        Huddl
        |> Ash.Query.for_read(:get_for_recurrence, %{id: draft.id})
        |> Ash.read_one!(authorize?: false)

      assert is_nil(deleted)
    end

    @tag :huddl_lifecycle
    test "cancels without deleting RSVP history and shows the explanation", %{
      conn: conn,
      owner: owner,
      member: member,
      group: group,
      huddl: huddl
    } do
      Communities.rsvp_huddl!(huddl, actor: member)

      session =
        conn
        |> login(owner)
        |> visit(~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
        |> click_button("Cancel huddl")
        |> within("#cancel-huddl-modal", fn modal ->
          modal
          |> fill_in("Explanation (optional)", with: "The venue lost power.")
          |> click_button("Cancel huddl")
        end)

      assert_path(session, ~p"/groups/#{group.slug}/huddlz/#{huddl.id}")
      assert_has(session, ".hero .eyebrow", text: "Cancelled")

      assert_has(session, "#cancellation-reason:has(+ .huddl-hero)")

      within(session, "#cancellation-reason", fn update ->
        update
        |> assert_has("h2", text: "Important update from the organizer")
        |> assert_has("p", text: "The venue lost power.")
      end)

      refute_has(session, "button", text: "Publish huddl")
      refute_has(session, "button", text: "Cancel huddl")
      refute_has(session, "a", text: "Edit huddl")

      reloaded = Communities.get_huddl!(huddl.id, actor: member)
      assert reloaded.lifecycle_state == :cancelled
      assert length(Communities.list_huddl_attendees!(huddl.id, actor: owner)) == 2
    end
  end

  defp huddl_still_exists?(id) do
    Huddl
    |> Ash.Query.for_read(:get_for_recurrence, %{id: id})
    |> Ash.read_one!(authorize?: false)
    |> is_struct(Huddl)
  end

  defp create_verified_user do
    User
    |> Ash.Changeset.for_create(:create, %{
      email: "user#{System.unique_integer()}@example.com",
      display_name: "Test User",
      role: :user
    })
    |> Ash.create!(authorize?: false)
  end
end
